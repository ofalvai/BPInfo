/*
 * Copyright 2016 Oliv√©r Falvai
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


// This file contains private information, create your own by renaming
// localconfig.example.properties in the project folder
def localConfig = new Properties()
localConfig.load(new FileInputStream(project.file("localconfig.properties")))

// Other variables are declared in gradle.properties

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId "com.example.bkkinfoplus"
        minSdkVersion 16 // Don't forget to change in the flavors too!
        targetSdkVersion 23
        versionName version_name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "hu"
        resValue "string", "APP_VERSION", version_name
        signingConfig signingConfigs.config
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            resValue "string", "PACKAGE_NAME_REAL", "com.example.bkkinfoplus.debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "PACKAGE_NAME_REAL", "com.example.bkkinfoplus"
        }
    }
    productFlavors {
        fastBuild {
            minSdkVersion 21
        }
        regular {
            minSdkVersion 16
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    apt 'com.google.dagger:dagger-compiler:2.5'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:customtabs:23.4.0'
    compile 'com.android.support:support-annotations:23.4.0'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.wefika:flowlayout:0.4.1'
    compile 'com.google.dagger:dagger:2.5'
    compile 'org.sufficientlysecure:html-textview:1.6'
    compile 'net.danlew:android.joda:2.9.4.1'
    compile 'com.jakewharton:process-phoenix:1.0.2'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        // Necessary if your app targets Marshmallow (since Espresso
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:runner:0.5') {
        // Necessary if your app targets Marshmallow (since the test runner
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        // Necessary if your app targets Marshmallow (since the test runner
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}
