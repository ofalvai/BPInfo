/*
 * Copyright 2016 OlivÃ©r Falvai
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.github.gfx.ribbonizer'
apply plugin: 'io.fabric'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


// This file contains private information, create your own by renaming
// localconfig.example.properties in the project folder
def localConfig = new Properties()
localConfig.load(new FileInputStream(project.file("localconfig.properties")))

// Other variables are declared in gradle.properties

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.ofalvai.bpinfo"
        signingConfig signingConfigs.config

        minSdkVersion 16
        targetSdkVersion 25

        versionCode version_code.toInteger()
        versionName version_name

        resConfigs "en", "hu"
        resValue "string", "APP_VERSION", version_name
        buildConfigField "String", "BACKEND_URL", "\"${localConfig.backendUrl}\""
        manifestPlaceholders = [fabricApiKey: localConfig.fabricApiKey]

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            resValue "string", "PACKAGE_NAME_REAL", "com.ofalvai.bpinfo.debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "PACKAGE_NAME_REAL", "com.ofalvai.bpinfo"
        }
    }
    productFlavors {
        prod {
        }
        mock {
            applicationIdSuffix ".mock"
        }
    }
}

ribbonizer {
    builder { variant, iconFile ->
        if (variant.flavorName == "mock") {
            return yellowRibbonFilter(variant, iconFile)
        } else if (variant.buildType.name == "debug") {
            return grayRibbonFilter(variant, iconFile)
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    apt 'com.google.dagger:dagger-compiler:2.8'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:customtabs:25.1.1'
    compile 'com.android.support:support-annotations:25.1.1'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.wefika:flowlayout:0.4.1'
    compile 'com.google.dagger:dagger:2.8'
    compile 'org.sufficientlysecure:html-textview:1.6'
    compile 'net.danlew:android.joda:2.9.4.1'
    compile 'com.jakewharton:process-phoenix:1.1.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        // Necessary if your app targets Marshmallow (since Espresso
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:runner:0.5') {
        // Necessary if your app targets Marshmallow (since the test runner
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        // Necessary if your app targets Marshmallow (since the test runner
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}
